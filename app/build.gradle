plugins {
    alias libs.plugins.android.application
    alias libs.plugins.navigation.safeargs
}

android {
    namespace 'xyz.zedler.patrick.tack'
    compileSdk 34

    defaultConfig {
        applicationId "xyz.zedler.patrick.tack"
        minSdk 21
        targetSdk 34
        versionCode 160 // last number is 0 for app release
        versionName "4.2.0"
        resourceConfigurations += ["cs", "de", "en", "zh-rCN", "zh-rHK", "zh-rTW"]
    }

    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate false
                optimizeCode true
                proguardFiles {
                    getDefaultProguardFile('proguard-android-optimize.txt')
                    'proguard-rules.pro'
                }
            }
            debuggable false
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    lint {
        abortOnError false
        disable 'MissingTranslation'
    }

    bundle {
        storeArchive {
            enable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_17
    }

    dependenciesInfo {
        includeInApk false
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.fragment
    implementation libs.navigation.fragment
    implementation libs.navigation.ui
    implementation libs.preference
    implementation libs.shapes
    implementation libs.material
}